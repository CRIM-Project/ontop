[PrefixDeclaration]
:		https://data.crimproject.org/
dbo:		http://dbpedia.org/ontology/
dbp:		http://dbpedia.org/property/
owl:		http://www.w3.org/2002/07/owl#
rdf:		http://www.w3.org/1999/02/22-rdf-syntax-ns#
wdt:		http://www.wikidata.org/prop/direct/
xml:		http://www.w3.org/XML/1998/namespace
xsd:		http://www.w3.org/2001/XMLSchema#
obda:		https://w3id.org/obda/vocabulary#
omac:		https://omac.crimproject.org/
rdfs:		http://www.w3.org/2000/01/rdf-schema#
skos:		http://www.w3.org/2004/02/skos/core#
swrl:		http://www.w3.org/2003/11/swrl#
viaf:		http://viaf.org/viaf/
swrla:		http://swrl.stanford.edu/ontologies/3.3/swrla.owl#
swrlb:		http://www.w3.org/2003/11/swrlb#
schema:		https://schema.org/
dcterms:		http://purl.org/dc/terms/

[MappingDeclaration] @collection [[
mappingId	MAPID-person
target		:person/{person_id} a schema:Person ; schema:name {name}^^xsd:string ; rdfs:label {name}^^xsd:string . 
source		select person_id, name from crim_crimperson order by person_id

mappingId	MAPID-person-viaf
target		:person/{person_id} a schema:Person ; schema:name {name}^^xsd:string ; rdfs:label {name}^^xsd:string ; wdt:P214 {remarks}^^xsd:anyURI . 
source		select person_id,name, remarks from crim_crimperson where remarks!=''

mappingId	MAPID-mass
target		:source/{mass_id} a omac:MusicComposition ; dcterms:title {title}^^xsd:string ; omac:hasComposer :person/{composer_id} ; omac:hasAttribute :attribute/genre-{mass_id} . :attribute/genre-{mass_id} a omac:GenreAttribute ; rdfs:label "genre of {mass_id}"^^xsd:string ; omac:hasAttributeValue :value/{genre_id} . :value/{genre_id} a omac:GenreValue ; rdfs:label {genre_id}^^xsd:string . :person/{composer_id} a schema:Person . 
source		select mass_id, title, composer_id, genre_id from crim_crimmass
			order by composer_id

mappingId	MAPID-works-other-than-masses
target		:source/{piece_id} a omac:MusicComposition ; dcterms:title {title}^^xsd:string ; dbp:fullTitle {full_title}^^xsd:string ; omac:hasComposer :person/{composer_id} ; omac:hasAttribute :attribute/genre-{piece_id} . :attribute/genre-{piece_id} a omac:GenreAttribute ; rdfs:label "genre of {piece_id}"^^xsd:string ; omac:hasAttributeValue :value/{genre_id} . :value/{genre_id} a omac:GenreValue ; rdfs:label {genre_id}^^xsd:string . :person/{composer_id} a schema:Person . 
source		select piece_id, title, full_title, composer_id, genre_id from crim_crimpiece where genre_id !='mass'

mappingId	MAPID-structure-observation
target		:claim/structure/{id} a omac:StructureClaim ; rdfs:label "claim of structure with CRIM id {id}"^^xsd:string ; omac:assertedBy :person/{observer_id} ; omac:hasMusicTextArg :segment/{piece_id}_{emamodified} ; omac:hasAttributeArg :attribute/schema-{piece_id}_{emamodified} ; omac:hasAttributeValueArg :value/{musical_type} . :person/{observer_id} a schema:Person . :attribute/schema-{piece_id}_{emamodified} a omac:SchemaAttribute ; omac:attributeOf :segment/{piece_id}_{emamodified} ; rdfs:label "schema attribute of segment :segment/{piece_id}_{emamodified}"^^xsd:string . :value/{musical_type} a omac:SchemaValue ; rdfs:label {musical_type}^^xsd:string . :segment/{piece_id}_{emamodified} a omac:MusicSegment ; omac:segmentOf :source/{piece_id} ; rdfs:label "segment of :source/{piece_id}"^^xsd:string ; omac:EMAidentifier {ema}^^xsd:string . 
source		select id,  md5(ema) as emamodified, ema, musical_type, observer_id, piece_id from crim_crimobservation where musical_type !='' order by piece_id

mappingId	MAPID-person-role
target		:person/{person_id} a schema:Person ; omac:hasRole :role/{role_type_id} . :role/{role_type_id} a omac:SocialRole ; rdfs:label {role_type_id}^^xsd:string . 
source		select person_id, role_type_id from crim_crimrole where role_type_id !=''

mappingId	MAPID-model for
target		:source/{model_piece_id} omac:modelFor :source/{derivative_piece_id} . 
source		select id, relationship_type, derivative_piece_id, model_piece_id
			from crim_crimrelationship

mappingId	MAPID-similarity-claim-derivative
target		:claim/similarity/{id} a omac:SimilarityClaim ; rdfs:label "claim of similarity with CRIM id {id}"^^xsd:string ; omac:assertedBy :person/{observer_id} ; omac:hasDerivativeArg :segment/{derivative_piece_id}_{emamodified} ; omac:hasSimilarityTypeArg :value/{relationship_type} . :value/{relationship_type} a omac:SimilarityType ; rdfs:label {relationship_type}^^xsd:string . :segment/{derivative_piece_id}_{emamodified} a omac:MusicSegment ; omac:segmentOf :source/{derivative_piece_id} ; omac:EMAidentifier {ema}^^xsd:string . 
source		select
			crim_crimrelationship.id,
			crim_crimrelationship.relationship_type,
			crim_crimrelationship.derivative_piece_id,
			crim_crimrelationship.observer_id,
			md5(crim_crimobservation.ema) as emamodified,
			crim_crimobservation.ema
			from crim_crimrelationship
			join crim_crimobservation
			on crim_crimrelationship.derivative_observation_id=crim_crimobservation.id;

mappingId	MAPID-similarity-claim-model
target		:claim/similarity/{id} a omac:SimilarityClaim ; omac:assertedBy :person/{observer_id} ; omac:hasModelArg :segment/{model_piece_id}_{emamodified} . :segment/{model_piece_id}_{emamodified} a omac:MusicSegment ; omac:segmentOf :source/{model_piece_id} ; omac:EMAidentifier {ema}^^xsd:string . 
source		select
			crim_crimrelationship.id,
			crim_crimrelationship.relationship_type,
			crim_crimrelationship.model_piece_id,
			crim_crimrelationship.observer_id,
			md5(crim_crimobservation.ema) as emamodified,
			crim_crimobservation.ema
			from crim_crimrelationship
			join crim_crimobservation
			on crim_crimrelationship.model_observation_id=crim_crimobservation.id;

mappingId	MAPID-mass-sections
target		:source/{piece_id} a omac:MusicSection ; dcterms:title {title}^^xsd:string ; dbp:fullTitle {full_title}^^xsd:string ; omac:sectionOf :source/{mass_id} ; omac:hasComposer :person/{composer_id} . :person/{composer_id} a schema:Person . 
source		select piece_id, full_title, title, mass_id, composer_id
			from crim_crimpiece where genre_id='mass' order by piece_id
]]

