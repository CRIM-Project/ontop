title="Ontop Portal for the CRIM Project"

[[tabGroups]]

name="Examples of SPARQL queries"

[[tabGroups.tabs]]

name="Compositions and their composers"
query="""
prefix rdfs: <http://www.w3.org/2000/01/rdf-schema#>
prefix dcterms: <http://purl.org/dc/terms/>
prefix data: <https://data.crimproject.org/>
prefix omac: <https://omac.crimproject.org/>
PREFIX schema: <https://schema.org/>

SELECT ?composition ?title ?composerName WHERE
{?composition a omac:MusicComposition; 
      dcterms:title ?title;
      omac:hasComposer/schema:name ?composerName. 

} order by ?composition
"""

[[tabGroups.tabs]]
name="Compositions and their genre"
query="""
prefix rdfs: <http://www.w3.org/2000/01/rdf-schema#>
prefix dcterms: <http://purl.org/dc/terms/>
prefix data: <https://data.crimproject.org/>
prefix omac: <https://omac.crimproject.org/>
PREFIX schema: <https://schema.org/>

SELECT ?composition ?title ?genreLabel ?composerName WHERE
{?composition a omac:MusicComposition; 
      dcterms:title ?title;
      omac:hasAttribute ?genre;
       omac:hasComposer/schema:name ?composerName.

?genre a omac:GenreAttribute; 
      omac:hasAttributeValue/rdfs:label ?genreLabel.

} order by ?genreLabel
"""

[[tabGroups.tabs]]
name="Compositions' genre other than mass"
query="""
prefix rdfs: <http://www.w3.org/2000/01/rdf-schema#>
prefix dcterms: <http://purl.org/dc/terms/>
prefix data: <https://data.crimproject.org/>
prefix omac: <https://omac.crimproject.org/>
PREFIX schema: <https://schema.org/>

SELECT ?composition ?title ?genreLabel ?composerName WHERE
{?composition a omac:MusicComposition; 
      dcterms:title ?title;
      omac:hasAttribute/omac:hasAttributeValue ?genreValue;
      omac:hasComposer/schema:name ?composerName.
  		?genreValue rdfs:label ?genreLabel.
      FILTER (?genreValue != <https://data.crimproject.org/genre/mass>)

} order by ?genreLabel
"""

[[tabGroups.tabs]]
name="Compositions and their sections"
query="""
prefix rdfs: <http://www.w3.org/2000/01/rdf-schema#>
prefix dcterms: <http://purl.org/dc/terms/>
prefix data: <https://data.crimproject.org/>
prefix omac: <https://omac.crimproject.org/>
PREFIX schema: <https://schema.org/>

SELECT ?composition ?title ?section ?sectionTitle WHERE
{?composition a omac:MusicComposition;
dcterms:title ?title;
omac:hasSection ?section.
?section a omac:MusicSection;
dcterms:title ?sectionTitle. 

} order by ?composition
"""

[[tabGroups.tabs]]
name="Claims"
query="""
prefix rdfs: <http://www.w3.org/2000/01/rdf-schema#>
prefix dcterms: <http://purl.org/dc/terms/>
prefix data: <https://data.crimproject.org/>
prefix omac: <https://omac.crimproject.org/>
PREFIX schema: <https://schema.org/>

SELECT ?claim ?subclass ?label ?observerName
WHERE
{?claim a omac:Claim; a ?subclass;
rdfs:label ?label;
omac:assertedBy/schema:name ?observerName. 
FILTER(?subclass != omac:Claim)
} order by ?subclass
"""

[[tabGroups.tabs]]
name="Claims of structure"
query="""
prefix rdfs: <http://www.w3.org/2000/01/rdf-schema#>
prefix dcterms: <http://purl.org/dc/terms/>
prefix data: <https://data.crimproject.org/>
prefix omac: <https://omac.crimproject.org/>
PREFIX schema: <https://schema.org/>

SELECT ?strClaim ?personName ?segment ?piece ?schemaValue WHERE
{?strClaim a omac:StructureClaim;
omac:assertedBy/schema:name ?personName;
omac:hasMusicTextArg ?segment;
omac:hasAttributeArg ?schema;
omac:hasAttributeValueArg ?schemaValue.

?schema omac:attributeOf ?segment.
?segment a omac:MusicSegment;
omac:segmentOf ?piece.

}order by ?segment
"""

[[tabGroups.tabs]]
name="Claims of similarity 1"
query="""

## This query retrieves the model and derivative *segments* identified in a similarity relationship

prefix rdfs: <http://www.w3.org/2000/01/rdf-schema#>
prefix dcterms: <http://purl.org/dc/terms/>
prefix data: <https://data.crimproject.org/>
prefix omac: <https://omac.crimproject.org/>
PREFIX schema: <https://schema.org/>

SELECT ?simClaim ?personName ?model ?derivative ?similarityLabel WHERE
{?simClaim a omac:SimilarityClaim;
omac:assertedBy/schema:name ?personName;
omac:hasModelArg ?model;
omac:hasDerivativeArg ?derivative;
omac:hasSimilarityTypeArg ?similarityType.
?similarityType rdfs:label ?similarityLabel.} 
"""

[[tabGroups.tabs]]
name="Claims of similarity 2"
query="""

## This query retrieves the model and derivative identified in a similarity relationship

prefix rdfs: <http://www.w3.org/2000/01/rdf-schema#>
prefix dcterms: <http://purl.org/dc/terms/>
prefix data: <https://data.crimproject.org/>
prefix omac: <https://omac.crimproject.org/>
PREFIX schema: <https://schema.org/>

SELECT ?simClaim ?personName ?modelTxt ?derivativeTxt ?similarityLabel WHERE
{?simClaim a omac:SimilarityClaim;
omac:assertedBy/schema:name ?personName;
omac:hasModelArg ?model;
omac:hasDerivativeArg ?derivative;
omac:hasSimilarityTypeArg ?similarityType.
?similarityType rdfs:label ?similarityLabel.

?model omac:segmentOf ?modelTxt.
?derivative omac:segmentOf ?derivativeTxt.

}
"""